TITLE: (M2) Latent Basis (modified);
DATA: FILE = "nlalt-mplus-data.txt";
VARIABLE: NAMES = uncchdid smoke age1 age2 age3 bw2 w1-w15 d1-d15 
    sbp17 dbp17;
  USEVARIABLES = bw2 w1-w12 d1-d12;
  MISSING = ALL(-9);
MODEL:

!Growth
WtIn BY w1-w12@1;
WtLn BY w1@0 w2-w11 w12@1;

!Autoregressive
w1 on bw2@0; w2 ON w1@0; w3 ON w2@0; w4 ON w3@0; w5 ON w4@0; 
w6 ON w5@0; w7 ON w6@0; w8 ON w7@0; w9 ON w8@0; w10 ON w9@0; 
w11 ON w10@0; w12 ON w11@0;

!Effects of deviation from age at given wave
w1 on d1; w2 ON d2; w3 ON d3; w4 ON d4; w5 ON d5; w6 ON d6; 
w7 ON d7; w8 ON d8; w9 ON d9; w10 ON d10; w11 ON d11; w12 ON d12; 

!Fix indicator intercepts and free latent means
[w1-w12@0]; [WtIn*]; [WtLn*];

!Fix variance of linear component to 0 (negative variance estimate when free)
WtLn@0;
WtLn WITH WtIn@0 bw2@0 d1@0 d2@0 d3@0 d4@0 d5@0 d6@0 d7@0 d8@0 d9@0 d10@0 d11@0 d12@0;

!Address missing data in exogenous covariates
bw2 d1-d12;

OUTPUT: STDYX;


